---------------------------------------------------------
-- Free graded monad generated by algebraic operations --
---------------------------------------------------------

module Semantics.Model.Example.TSets.Monad.Effects.Properties.Naturality where

open import Function

open import Data.Empty
open import Data.Product
open import Data.Unit hiding (_≤_)

open import Semantics.Model.Example.TSets.TSets
open import Semantics.Model.Example.TSets.Modality.Future
open import Semantics.Model.Example.TSets.Modality.Past
open import Semantics.Model.Example.TSets.Monad.Core
open import Semantics.Model.Example.TSets.Monad.Effects

open import Semantics.Model.Category.Derived TSetCat renaming (map⇒ᵐ to map⇒ᵗ)

open import Util.Equality
open import Util.Operations
open import Util.Time

-- The (algebraic) operations of the monad generated by the operations in Op (continued)
----------------------------------------------------------------------------------------

-- Naturality of the delay operation

delayᵀ-nat : ∀ {A B} (τ : Time) {τ'}
           → (f : A →ᵗ B)
           →  delayᵀ τ {τ' = τ'} ∘ᵗ [ τ ]ᶠ (Tᶠ f)
           ≡ᵗ Tᶠ f ∘ᵗ delayᵀ τ
delayᵀ-nat τ f =
  eqᵗ (λ c → refl)

-- Naturality of the (other) algebraic operations

opᵀ-nat : ∀ {A B τ} → (op : Op)
        → (f : A →ᵗ B)
        →  opᵀ {τ = τ} op ∘ᵗ mapˣᵗ idᵗ ([ op-time op ]ᶠ (map⇒ᵗ idᵗ (Tᶠ f)))
        ≡ᵗ Tᶠ f ∘ᵗ opᵀ op
opᵀ-nat {A} {B} {τ} op f =
  eqᵗ (λ {t} c → 
    dcong₂ (op-node op (proj₁ c))
      (ifun-ext (fun-ext (λ p → fun-ext (λ y →
        cong (Tˢᶠ f) (cong (map-carrier (proj₂ c))
          (cong (_, y) (≤-irrelevant _ _)))))))
      (ifun-ext (ifun-ext (fun-ext λ p → fun-ext (λ q → fun-ext (λ y → uip))))))
