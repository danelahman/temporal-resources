---------------------------------------------------------
-- Free graded monad generated by algebraic operations --
---------------------------------------------------------

module Semantics.Model.Examples.TSets.Monad.Strength.Properties where

open import Function

open import Data.Empty
open import Data.Product
open import Data.Unit hiding (_≤_)

open import Semantics.Model.Examples.TSets.TSets
open import Semantics.Model.Examples.TSets.Modality.Future
open import Semantics.Model.Examples.TSets.Modality.Past
open import Semantics.Model.Examples.TSets.Modality.Adjunction
open import Semantics.Model.Examples.TSets.Monad.Core
open import Semantics.Model.Examples.TSets.Monad.Strength
open import Semantics.Model.Examples.TSets.Monad.Effects

open import Semantics.Model.Modality.Adjunction.Derived TSetCat TSetFut TSetPas TSetAdj

open import Util.Equality
open import Util.Operations
open import Util.Time

-- Strength's interaction with ηᵀ

strᵀ-ηᵀ : ∀ {A B}
        → strᵀ ∘ᵗ mapˣᵗ ε⁻¹ ηᵀ
       ≡ᵗ ηᵀ {A ×ᵗ B}

strᵀ-ηᵀ {A} {B} =
  eqᵗ λ { {t} (x , y) →
    cong leaf
      (cong (_, y)
        (trans
          (monotone-trans A _ _ _)
          (trans
            (cong (λ p → monotone A p x) (≤-irrelevant _ _))
            (monotone-refl A x)))) }

-- Strength's interaction with μᵀ

strˢ-μˢ : ∀ {A B τ τ' t}
        → (x : carrier A (t + τ + τ'))
        → (c : Tˢ (Tᵒ B τ') τ t)
        → μˢ (Tˢᶠ strᵀ (strˢ x c))
        ≡ strˢ {A} {B} (monotone A (≤-reflexive (+-assoc t τ τ')) x) (μˢ c)

strˢ-μˢ {A} {B} {_} {τ'} {t} v (leaf w) =
  cong (λ p → strˢ p w) (cong (λ p → monotone A p v) (≤-irrelevant _ _))
strˢ-μˢ {A} {B} {_} {τ'} {t} v (op-node op w k k-nat) =
  {!!}
strˢ-μˢ {A} {B} {_} {τ''} {t} v (delay-node {τ' = τ'} τ k) =
  trans
    (cong (τ-substˢ (sym (+-assoc τ τ' τ''))) (cong (delay-node τ)
      (strˢ-μˢ (monotone ([ τ'' ]ᵒ A) (≤-reflexive (sym (+-assoc t τ τ'))) v) k)))
    {!!}

strᵀ-μᵀ : ∀ {A B τ τ'}
        → μᵀ {A ×ᵗ B} {τ} {τ'} ∘ᵗ Tᶠ strᵀ ∘ᵗ strᵀ
       ≡ᵗ strᵀ ∘ᵗ mapˣᵗ (δ⁻¹ {A}) μᵀ

strᵀ-μᵀ {A} {B} {τ} {τ'} =
  eqᵗ (λ { {t} (x , c) → strˢ-μˢ x c })

-- Strength's interaction with sndᵗ

strˢ-sndᵗ : ∀ {A B τ t}
          → (v : carrier A (t + τ))
          → (c : Tˢ B τ t)
          → Tˢᶠ sndᵗ (strˢ {A} v c)
          ≡ c
          
strˢ-sndᵗ v (leaf w) =
  refl
strˢ-sndᵗ {A} {_} {_} {t} v (op-node {τ = τ} op w k k-nat) =
  dcong₂ (op-node op w)
    (ifun-ext (fun-ext (λ p → fun-ext (λ y →
      strˢ-sndᵗ
        (monotone A (≤-trans (≤-reflexive (sym (+-assoc t (op-time op) τ))) (+-monoˡ-≤ τ p)) v)
        (k p y)))))
    (ifun-ext (ifun-ext (fun-ext (λ p → fun-ext λ q → fun-ext λ y → uip))))
strˢ-sndᵗ {A} {_} {τ''} {t} v (delay-node {τ' = τ'} τ k) =
  cong (delay-node τ) (strˢ-sndᵗ (monotone A (≤-reflexive (sym (+-assoc t τ τ'))) v) k)

strᵀ-sndᵗ : ∀ {A B τ}
          → Tᶠ sndᵗ ∘ᵗ strᵀ {A} {B} {τ}
         ≡ᵗ sndᵗ

strᵀ-sndᵗ {A} {B} {τ} =
  eqᵗ λ { {t} (x , c) → strˢ-sndᵗ x c }

